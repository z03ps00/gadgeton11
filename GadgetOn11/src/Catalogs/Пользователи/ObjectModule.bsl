Процедура ЗаполнитьНаборРолей(ПользовательИБ) Экспорт
	РолиИБ = Метаданные.Роли;
	
	ЭтотОбъект.НаборРолей.Очистить();
	Для Каждого Роль Из РолиИБ Цикл
		СтрокаТЧ = ЭтотОбъект.НаборРолей.Добавить();
		СтрокаТЧ.ИмяРоли 			= Роль.Имя;
		СтрокаТЧ.ПредставлениеРоли 	= Роль.Синоним;
		СтрокаТЧ.Активна 			= ПользовательИБ.Роли.Содержит(Роль);
	КонецЦикла;	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(ЭтотОбъект.Наименование);
	
	Если ПользовательИБ = Неопределено Тогда
		ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
		ПользовательИБ.Имя 			= ЭтотОбъект.Наименование;
		ПользовательИБ.ПолноеИмя 	= ЭтотОбъект.ПолноеИмя;		
	КонецЕсли;
	
	ПользовательИБ.Роли.Очистить();
	АктивныеРоли = ЭтотОбъект.НаборРолей.НайтиСтроки(Новый Структура("Активна", Истина));
	
	Для Каждого СтрокаТЧ Из АктивныеРоли Цикл
		ТекРоль = Метаданные.Роли.Найти(СтрокаТЧ.ИмяРоли);		
		ПользовательИБ.Роли.Добавить(ТекРоль);
	КонецЦикла;
	
	ПользовательИБ.Записать();
КонецПроцедуры
